
{ Global compiler directives }
{$include bold.inc}
unit BoldFireDACInterfaces;

interface

{$M-}  // Reset RTTI state (BoldDBInterfaces enables {$M+} for interface mocking)

uses
  Classes,
  Db,
  SysUtils,

  FireDAC.Comp.Client,
  FireDAC.Stan.Param,

  BoldSQLDatabaseConfig,
  BoldDBInterfaces,
  BoldDefs;

type
  { forward declarations }
  TBoldFireDACParameter = class;
  TBoldFireDACQuery = class;
  TBoldFireDACTable = class;
  TBoldFireDACConnection = class;

  TFireDacParam = TFDParam;

  TBoldFireDACQueryClass = class of TBoldFireDACQuery;
  TBoldFireDACExecQueryClass = class of TBoldFireDACExecQuery;

  { TBoldFireDACParameter }
  TBoldFireDACParameter = class(TBoldParameterWrapper, IBoldParameter)
  private
    fFDParam: TFireDacParam;
    function GetAsVariant: Variant;
    procedure SetAsVariant(const NewValue: Variant);
    function GetName: string;
    procedure Clear;
    function GetDataType: TFieldType;
    procedure SetDataType(Value: TFieldType);
    function GetAsBCD: Currency;
    function GetAsblob: TBoldBlobData;
    function GetAsBoolean: Boolean;
    function GetAsDateTime: TDateTime;
    function GetAsCurrency: Currency;
    function GetAsFloat: Double;
    function GetAsInteger: Longint;
    function GetAsInt64: Int64;
    function GetAsMemo: string;
    function GetAsString: string;
    function GetIsNull: Boolean;
    function GetAsWideString: WideString;
    procedure SetAsBCD(const Value: Currency);
    procedure SetAsBlob(const Value: TBoldBlobData);
    procedure SetAsBoolean(Value: Boolean);
    procedure SetAsCurrency(const Value: Currency);
    procedure SetAsDate(const Value: TDateTime);
    procedure SetAsDateTime(const Value: TDateTime);
    procedure SetAsFloat(const Value: Double);
    procedure SetAsInteger(Value: Longint);
    procedure SetAsInt64(const Value: Int64);
    procedure SetAsMemo(const Value: string);
    procedure SetAsString(const Value: string);
    procedure SetAsSmallInt(Value: Longint);
    procedure SetAsTime(const Value: TDateTime);
    procedure SetAsWord(Value: Longint);
    procedure SetText(const Value: string);
    procedure SetAsWideString(const Value: Widestring);
    function GetAsAnsiString: TBoldAnsiString;
    procedure SetAsAnsiString(const Value: TBoldAnsiString);
    function GetFDParam: TFireDacParam;
    procedure AssignFieldValue(const source: IBoldField);
    procedure Assign(const source: IBoldParameter);
    property FDParam: TFireDacParam read GetFDParam;
  public
    constructor Create(FireDACParameter: TFireDacParam; DatasetWrapper: TBoldAbstractQueryWrapper);
  end;

  { TBoldFireDACQuery }
  TBoldFireDACQuery = class(TBoldBatchDataSetWrapper, IBoldQuery, IBoldExecQuery, IBoldParameterized)
  private
    fQuery: TFDQuery;
    fReadTransactionStarted: Boolean;
    fUseReadTransactions: boolean;
    function GetQuery: TFDQuery;
    procedure AssignParams(Sourceparams: TParams);
    function GetParamCount: Integer;
    function GetParam(i: Integer): IBoldParameter;
    function GetParamCheck: Boolean;
    procedure SetParamCheck(value: Boolean);
    function GetRequestLiveQuery: Boolean;
    procedure SetRequestLiveQuery(NewValue: Boolean);
    procedure AssignSQL(SQL: TStrings); virtual;
    function GetRecordCount: Integer;
    function GetUseReadTransactions: boolean;
    procedure SetUseReadTransactions(value: boolean);
    procedure BeginExecuteQuery;
    procedure EndExecuteQuery;
  protected
    function ParamByName(const Value: string): IBoldParameter; override;
    function FindParam(const Value: string): IBoldParameter; override;
    function CreateParam(FldType: TFieldType; const ParamName: string; ParamType: TParamType; Size: integer): IBoldParameter; override;
    function GetParams: TParams; override;
    function GetSqlText: string; override;
    function GetSQLStrings: TStrings; override;
    procedure AssignSQLText(const SQL: string); override;
    function GetRowsAffected: Integer;
    function GetDataSet: TDataSet; override;
    procedure Prepare;
    procedure ClearParams;
    procedure Open; override;
    procedure Close; override;
    procedure ExecSQL; override;
    function GetRecNo: integer; override;
    property Query: TFDQuery read GetQuery;
  public
    constructor Create(BoldFireDACConnection: TBoldFireDACConnection); reintroduce;
    destructor Destroy; override;
    procedure Clear; override;
  end;

  { TBoldFireDACExecQuery }
  TBoldFireDACExecQuery = class(TBoldBatchDataSetWrapper, IBoldExecQuery, IBoldParameterized)
  private
    fExecQuery: TFDQuery;
    fReadTransactionStarted: Boolean;
    fUseReadTransactions: boolean;
  protected
    function GetExecQuery: TFDQuery;
    function GetParams: TParams; override;
    procedure AssignParams(Sourceparams: TParams);
    function GetParamCount: Integer;
    function GetParam(i: Integer): IBoldParameter;
    function GetParamCheck: Boolean;
    procedure SetParamCheck(value: Boolean);
    function ParamByName(const Value: string): IBoldParameter; override;
    function FindParam(const Value: string): IBoldParameter; override;
    function CreateParam(FldType: TFieldType; const ParamName: string): IBoldParameter; overload; override;
    function CreateParam(FldType: TFieldType; const ParamName: string; ParamType: TParamType; Size: integer): IBoldParameter; overload; override;
    function EnsureParamByName(const Value: string): IBoldParameter; override;
    function GetSqlText: string; override;
    function GetSQLStrings: TStrings; override;
    procedure AssignSQL(SQL: TStrings); virtual;
    procedure AssignSQLText(const SQL: string); override;
    function GetRowsAffected: Integer;
    function GetUseReadTransactions: boolean;
    procedure SetUseReadTransactions(value: boolean);
    procedure BeginExecuteQuery;
    procedure EndExecuteQuery;
    function GetBatchQueryParamCount: integer;
    procedure Prepare;
    function GetDataSet: TDataSet; override;
    procedure ClearParams;
    procedure ExecSQL; override;
    property ExecQuery: TFDQuery read GetExecQuery;
  public
    constructor Create(BoldFireDACConnection: TBoldFireDACConnection); reintroduce;
    destructor Destroy; override;
    procedure Clear; override;
  end;

  { TBoldFireDACTable }
  TBoldFireDACTable = class(TBoldDatasetWrapper, IBoldTable)
  private
    fFDTable: TFDTable;
    function GetFDTable: TFDTable;
    property FDTable: TFDTable read GetFDTable;
    procedure AddIndex(const Name, Fields: string; Options: TIndexOptions; const DescFields: string = '');
    procedure CreateTable;
    procedure DeleteTable;
    function GetIndexDefs: TIndexDefs;
    procedure SetTableName(const NewName: string);
    function GetTableName: string;
    procedure SetExclusive(NewValue: Boolean);
    function GetExclusive: Boolean;
    function GetExists: Boolean;
  protected
    function GetDataSet: TDataSet; override;
    function ParamByName(const Value: string): IBoldParameter; override;
    function FindParam(const Value: string): IBoldParameter; override;
  public
    constructor Create(aFDTable: TFDTable; BoldFireDACConnection: TBoldFireDACConnection); reintroduce;
    destructor Destroy; override;
  end;

  { TBoldFireDACConnection }
  TBoldFireDACConnection = class(TBoldDatabaseWrapper, IBoldDataBase)
  private
    fFDConnection: TFDConnection;
    fOwnsConnection: Boolean;
    fCachedTable: TBoldFireDACTable;
    fCachedQuery1: TBoldFireDACQuery;
    fCachedQuery2: TBoldFireDACQuery;
    fCachedExecQuery1: TBoldFireDACQuery;
    fExecuteQueryCount: integer;
    function GetFDConnection: TFDConnection;
    property FDConnection: TFDConnection read GetFDConnection;
    function GetConnected: Boolean;
    function GetInTransaction: Boolean;
    function GetIsSQLBased: Boolean;
    procedure SetlogInPrompt(NewValue: Boolean);
    function GetLogInPrompt: Boolean;
    procedure SetKeepConnection(NewValue: Boolean);
    function GetKeepConnection: Boolean;
    procedure StartTransaction;
    procedure StartReadTransaction;
    procedure Commit;
    procedure RollBack;
    procedure Open;
    procedure Close;
    procedure Reconnect;
    function SupportsTableCreation: Boolean;
    procedure ReleaseCachedObjects;
    function GetIsExecutingQuery: Boolean;
    procedure BeginExecuteQuery;
    procedure EndExecuteQuery;
  private
    function GetTransaction: TFDTransaction;
    function GetUpdateTransaction: TFDTransaction;
    procedure SetTransaction(const Value: TFDTransaction);
    procedure SetUpdateTransaction(const Value: TFDTransaction);
    function CreateAnotherDatabaseConnection: IBoldDatabase;
    function GetImplementor: TObject;
  protected
    procedure AllTableNames(Pattern: string; ShowSystemTables: Boolean; TableNameList: TStrings); override;
    function GetTable: IBoldTable; override;
    function GetQuery: IBoldQuery; override;
    function GetExecQuery: IBoldExecQuery; override;
    procedure ReleaseTable(var Table: IBoldTable); override;
    procedure ReleaseQuery(var Query: IBoldQuery); override;
    procedure ReleaseExecQuery(var Query: IBoldExecQuery); override;
    function TableExists(const TableName: String): Boolean; override;
    property Transaction: TFDTransaction read GetTransaction write SetTransaction;
    property UpdateTransaction: TFDTransaction read GetUpdateTransaction write SetUpdateTransaction;
  public
    constructor Create(aFDConnection: TFDConnection; SQLDataBaseConfig: TBoldSQLDatabaseConfig);
    destructor Destroy; override;
    procedure CreateDatabase(DropExisting: boolean = true); override;
    procedure DropDatabase; override;
    function DatabaseExists: boolean; override;
    function GetDatabaseError(const E: Exception; const sSQL: string = ''):
        EBoldDatabaseError;
    property ExecuteQueryCount: integer read fExecuteQueryCount;
  end;

var
  BoldFireDACQueryClass: TBoldFireDACQueryClass = TBoldFireDACQuery;
  BoldFireDACExecQueryClass: TBoldFireDACExecQueryClass = TBoldFireDACExecQuery;

implementation

uses
  Variants,
  Masks,

  FireDAC.Stan.Option,
  FireDAC.Comp.Script,
  FireDAC.Comp.ScriptCommands,
  FireDAC.Phys.Intf,
  FireDAC.Phys.PGWrapper,
  FireDAC.Stan.Intf,

  BoldUtils,
  BoldGuard,
  BoldCoreConsts;

{ TBoldFireDACQuery }

function TBoldFireDACQuery.GetQuery: TFDQuery;
begin
  Result := fQuery;
end;

function TBoldFireDACQuery.GetDataSet: TDataSet;
begin
  Result := Query;
end;

function TBoldFireDACQuery.GetParamCheck: Boolean;
begin
  result := Query.ResourceOptions.ParamCreate;
end;

function TBoldFireDACQuery.GetParamCount: Integer;
begin
  Result := Query.Params.Count;
end;

type TFDAdaptedDataSetAccess = class(TFDAdaptedDataSet);

function TBoldFireDACQuery.GetParams: TParams;
begin
  result := TFDAdaptedDataSetAccess(Query).PSGetParams;
end;

function TBoldFireDACQuery.GetParam(i: Integer): IBoldParameter;
begin
  Result := TBoldFireDACParameter.Create(Query.Params[i], Self);
end;

function TBoldFireDACQuery.GetRecNo: integer;
begin
  result := Query.RecNo - 1;
end;

function TBoldFireDACQuery.GetRecordCount: Integer;
begin
  Result := Query.RecordCount;
end;

function TBoldFireDACQuery.GetRequestLiveQuery: Boolean;
begin
  Result := False;
end;

function TBoldFireDACQuery.GetRowsAffected: Integer;
begin
  result := Query.RowsAffected;
end;

function TBoldFireDACQuery.GetSQLStrings: TStrings;
begin
  result := Query.SQL;
end;

function TBoldFireDACQuery.GetSQLText: string;
begin
  Result := Query.SQL.Text;
end;

function TBoldFireDACQuery.GetUseReadTransactions: boolean;
begin
  result := fUseReadTransactions;
end;

procedure TBoldFireDACQuery.AssignParams(Sourceparams: TParams);
var
  lIndexSourceParams: Integer;
  lFDParam: TFireDacParam;
begin
  Query.Params.Clear;
  if Assigned(Sourceparams) and (Sourceparams.Count > 0) then
  begin
    for lIndexSourceParams := 0 to Sourceparams.Count - 1 do
    begin
      lFDParam := Query.Params.CreateParam(Sourceparams[lIndexSourceParams].DataType, Sourceparams[lIndexSourceParams].Name, Sourceparams[lIndexSourceParams].ParamType) as TFireDacParam;
      lFDParam.Value := Sourceparams[lIndexSourceParams].Value;
    end;
  end;
end;

procedure TBoldFireDACQuery.AssignSQL(SQL: TStrings);
begin
  Query.SQL.Assign(SQL);
  //function ParseSQL(SQL: WideString; DoCreate: Boolean): WideString;
  //DoCreate indicates whether to clear all existing parameter definitions before parsing the SQL statement.
end;

procedure TBoldFireDACQuery.AssignSQLText(const SQL: string);
begin
  Query.SQL.Text := Sql;
end;

procedure TBoldFireDACQuery.BeginExecuteQuery;
begin
  (DatabaseWrapper as TBoldFireDACConnection).BeginExecuteQuery;
end;

procedure TBoldFireDACQuery.Clear;
begin
  AssignSQLText('');
  ClearParams;
end;

procedure TBoldFireDACQuery.ClearParams;
begin
  Query.Params.Clear;
end;

procedure TBoldFireDACQuery.Close;
begin
  inherited;
  if (fReadTransactionStarted) and (DatabaseWrapper as TBoldFireDACConnection).GetInTransaction then
    (DatabaseWrapper as TBoldFireDACConnection).Commit;
  fReadTransactionStarted := false;
end;

constructor TBoldFireDACQuery.Create(BoldFireDACConnection: TBoldFireDACConnection);
begin
  inherited Create(BoldFireDACConnection);
  fUseReadTransactions := true;
  fQuery := TFDQuery.Create(nil);
  fQuery.Connection := (DatabaseWrapper as TBoldFireDACConnection).FDConnection;
end;

function TBoldFireDACQuery.CreateParam(FldType: TFieldType; const ParamName: string; ParamType: TParamType; Size: integer): IBoldParameter;
var
  lFDParam: TFireDacParam;
begin
  lFDParam := Query.Params.CreateParam(FldType, ParamName, ptUnknown) as TFireDacParam;
//  lFDParam.Size := Size;
  lFDParam.Value := NULL;
  Result := TBoldFireDACParameter.Create(lFDParam, Self);
end;

destructor TBoldFireDACQuery.Destroy;
begin
  if (fReadTransactionStarted) then
    Close;
  FreeAndNil(fQuery);
  inherited;
end;

procedure TBoldFireDACQuery.EndExecuteQuery;
begin
  (DatabaseWrapper as TBoldFireDACConnection).EndExecuteQuery;
end;

type TStringsAccess = class(TStrings);

procedure TBoldFireDACQuery.ExecSQL;
begin
  if InBatch then
  begin
    BatchExecSQL;
    exit;
  end;
  BeginExecuteQuery;
  try
    BoldLogSQLWithParams(Query.SQL, self);
    try
      if (DatabaseWrapper as TBoldFireDACConnection).GetInTransaction then
        fReadTransactionStarted := false
      else
      begin
        if fUseReadTransactions then
          (DatabaseWrapper as TBoldFireDACConnection).StartReadTransaction;
        fReadTransactionStarted := fUseReadTransactions;
      end;
      Query.Execute;
      if fReadTransactionStarted and (DatabaseWrapper as TBoldFireDACConnection).GetInTransaction then
      begin
        (DatabaseWrapper as TBoldFireDACConnection).Commit;
        fReadTransactionStarted := false;
      end;
    except
      on E: Exception do
      begin
        if (DatabaseWrapper as TBoldFireDACConnection).GetInTransaction then
          (DatabaseWrapper as TBoldFireDACConnection).Rollback;
        raise TBoldFireDACConnection(DatabaseWrapper).GetDatabaseError(E, Query.SQL.Text);
      end;
    end;
  finally
    EndExecuteQuery;
  end;
end;

function TBoldFireDACQuery.FindParam(const Value: string): IBoldParameter;
var
  Param: TFireDacParam;
begin
  result := nil;
  Param := Query.FindParam(Value);
  if Assigned(Param) then
    Result := TBoldFireDACParameter.Create(Param, Self);
end;

procedure TBoldFireDACQuery.Open;
begin
  BeginExecuteQuery;
  try
    BoldLogSQLWithParams(Query.SQL, self);
    try
      if (DatabaseWrapper as TBoldFireDACConnection).GetInTransaction then
        fReadTransactionStarted := false
      else
      begin
        if fUseReadTransactions then
          (DatabaseWrapper as TBoldFireDACConnection).StartReadTransaction;
        fReadTransactionStarted := fUseReadTransactions;
      end;
      Query.UpdateOptions.ReadOnly := true;
      inherited;
    except
      on E: Exception do
      begin
        if (DatabaseWrapper as TBoldFireDACConnection).GetInTransaction then
          (DatabaseWrapper as TBoldFireDACConnection).Rollback;
        raise TBoldFireDACConnection(DatabaseWrapper).GetDatabaseError(E, Query.SQL.Text);
      end;
    end;
  finally
    EndExecuteQuery;
  end;
end;

function TBoldFireDACQuery.ParamByName(const Value: string): IBoldParameter;
var
  lFDParam: TFireDacParam;
begin
  lFDParam := Query.Params.ParamByName(Value);
  Result := TBoldFireDACParameter.Create(lFDParam, Self)
end;

procedure TBoldFireDACQuery.Prepare;
begin
  Query.Prepare;
end;

procedure TBoldFireDACQuery.SetParamCheck(value: Boolean);
begin
  Query.ResourceOptions.ParamCreate := Value;
end;

procedure TBoldFireDACQuery.SetRequestLiveQuery(NewValue: Boolean);
begin
  // ignore
end;

procedure TBoldFireDACQuery.SetUseReadTransactions(value: boolean);
begin
  fUseReadTransactions := value;
end;

{ TBoldFireDACTable }

constructor TBoldFireDACTable.Create(aFDTable: TFDTable; BoldFireDACConnection: TBoldFireDACConnection);
begin
  inherited Create(BoldFireDACConnection);
  fFDTable := aFDTable;
end;

destructor TBoldFireDACTable.Destroy;
begin
  FreeAndNil(fFDTable);
  inherited;
end;

procedure TBoldFireDACTable.CreateTable;
begin
  // Not used - SupportsTableCreation returns False, so Bold uses SQL generation instead
  raise EBold.CreateFmt('MethodNotImplemented', [ClassName, 'CreateTable']);
end;

procedure TBoldFireDACTable.DeleteTable;
begin
  // Not used - SupportsTableCreation returns False, so Bold uses SQL generation instead
  raise EBold.CreateFmt('MethodNotImplemented', [ClassName, 'DeleteTable']);
end;

function TBoldFireDACTable.FindParam(const Value: string): IBoldParameter;
begin
  // Required by base class but never called for IBoldTable
  Result := nil;
end;

procedure TBoldFireDACTable.AddIndex(const Name, Fields: string;
  Options: TIndexOptions; const DescFields: string);
begin
  // Not used - SupportsTableCreation returns False, so Bold uses SQL generation instead
  raise EBold.CreateFmt('MethodNotImplemented', [ClassName, 'AddIndex']);
end;

function TBoldFireDACTable.GetDataSet: TDataSet;
begin
  Result := fFDTable;
end;

function TBoldFireDACTable.GetExclusive: Boolean;
begin
  Result := False;
end;

function TBoldFireDACTable.GetExists: Boolean;
var
  lAllTables: TStringList;
  lGuard: IBoldGuard;
begin
  Result := False;
  if Assigned(FDTable) and Assigned(FDTable.Connection) then
  begin
    lGuard := TBoldGuard.Create(lAllTables);
    lAllTables := TStringList.Create;
    FDTable.Connection.GetTableNames('', '', '', lAllTables, [osMy], [tkTable], false);
    Result := lAllTables.IndexOf(GetTableName) <> -1;
  end;
end;

function TBoldFireDACTable.GetIndexDefs: TIndexDefs;
begin
  Result := FDTable.IndexDefs;
end;

function TBoldFireDACTable.GetFDTable: TFDTable;
begin
  Result := fFDTable;
end;

function TBoldFireDACTable.ParamByName(const Value: string): IBoldParameter;
begin
  // Required by base class but never called for IBoldTable
  Result := nil;
end;

function TBoldFireDACTable.GetTableName: string;
begin
  Result := FDTable.TableName;
end;

procedure TBoldFireDACTable.SetExclusive(NewValue: Boolean);
begin
end;

procedure TBoldFireDACTable.SetTableName(const NewName: string);
begin
  FDTable.TableName := NewName;
end;

{ TBoldFireDACConnection }

// Populate the "TableNameList" with tablenames from the database that maches "pattern"

procedure TBoldFireDACConnection.AllTableNames(Pattern: string; ShowSystemTables: Boolean; TableNameList: TStrings);
var
  lTempList: TStringList;
  lIndexTempList: Integer;
  lGuard: IBoldGuard;
  i: integer;
  CatalogName: string;
begin
  lGuard := TBoldGuard.Create(lTempList);
  lTempList := TStringList.Create;

  // For SQLite, don't pass database filename as catalog - it causes invalid SQL
  // like "FROM bolddemo.db.sqlite_master" instead of "FROM sqlite_master"
  if SameText(FDConnection.Params.DriverID, 'SQLite') then
    CatalogName := ''
  else
    CatalogName := FDConnection.Params.Database;

  if ShowSystemTables then
    FDConnection.GetTableNames(CatalogName,'','',lTempList, [osMy, osSystem, osOther], [tkTable])
  else
    FDConnection.GetTableNames(CatalogName,'','',lTempList, [osMy], [tkTable]);

  // convert from fully qualified names in format: database.catalogue.table to just table name
  for i := 0 to lTempList.Count - 1 do
    while pos('.', lTempList[i]) > 0 do
      lTempList[i] := Copy(lTempList[i], pos('.', lTempList[i])+1, maxInt);

  if Pattern = '' then
    TableNameList.Assign(lTempList)
  else
  // MatchesMask is used to compare filenames with wildcards, suits us here
  // but there should be some care taken, when using tablenames with period
  // signes, as that might be interpreted as filename extensions
  for lIndexTempList := 0 to lTempList.Count - 1 do
  begin
    if MatchesMask(lTempList[lIndexTempList], Pattern) then
    begin
      TableNameList.Add(lTempList[lIndexTempList]);
    end;
  end;
end;

function TBoldFireDACConnection.TableExists(const TableName: String): Boolean;
begin
  // For Oracle, we need to check ALL_TABLES with OWNER filter because:
  // 1. We might be connected as admin (SYSTEM) checking for tables in another schema
  // 2. The default AllTableNames only shows current user's tables
  if GetSQLDatabaseConfig.Engine = dbeOracle then
  begin
    var Query: IBoldQuery;
    var SchemaOwner: string;
    SchemaOwner := FDConnection.Params.UserName;
    Query := GetQuery;
    try
      Query.AssignSQLText(
        'SELECT TABLE_NAME FROM ALL_TABLES WHERE OWNER = UPPER(''' + SchemaOwner + ''') AND TABLE_NAME = UPPER(''' + TableName + ''')');
      Query.Open;
      Result := not Query.Eof;
      Query.Close;
    finally
      ReleaseQuery(Query);
    end;
  end
  else
    Result := inherited TableExists(TableName);
end;

procedure TBoldFireDACConnection.Commit;
begin
  FDConnection.Commit;
end;

function TBoldFireDACConnection.GetImplementor: TObject;
begin
  result := FDConnection;
end;

function TBoldFireDACConnection.GetInTransaction: Boolean;
begin
  Result := FDConnection.InTransaction;
end;

function TBoldFireDACConnection.GetIsExecutingQuery: Boolean;
begin
  Result := fExecuteQueryCount > 0;
end;

function TBoldFireDACConnection.GetIsSQLBased: Boolean;
begin
  Result := True;
end;

function TBoldFireDACConnection.GetKeepConnection: Boolean;
begin
  //CheckMe;
  Result := True;
end;

function TBoldFireDACConnection.GetLogInPrompt: Boolean;
begin
  Result := FDConnection.LoginPrompt;
end;

procedure TBoldFireDACConnection.RollBack;
begin
  FDConnection.RollBack;
end;

procedure TBoldFireDACConnection.SetKeepConnection(NewValue: Boolean);
begin
  //CheckMe;
end;

procedure TBoldFireDACConnection.SetlogInPrompt(NewValue: Boolean);
begin
  FDConnection.LoginPrompt := NewValue;
end;

procedure TBoldFireDACConnection.SetTransaction(const Value: TFDTransaction);
begin
  FDConnection.Transaction := Value;
end;

procedure TBoldFireDACConnection.SetUpdateTransaction(
  const Value: TFDTransaction);
begin
  FDConnection.UpdateTransaction := value;
end;

procedure TBoldFireDACConnection.StartReadTransaction;
begin
  Transaction.Options.Isolation  := xiReadCommitted;
  FDConnection.StartTransaction;
end;

procedure TBoldFireDACConnection.StartTransaction;
begin
  Transaction.Options.Isolation := xiRepeatableRead;
  FDConnection.StartTransaction;
end;

function TBoldFireDACConnection.DatabaseExists: boolean;
var
  vQuery: IBoldQuery;
  vDatabaseName: string;
begin
  vDatabaseName := FDConnection.Params.Database;
  FDConnection.Connected := False;
  FDConnection.Params.Database := ''; // need to clear this to connect successfully
  vQuery := GetQuery;
  try
    vQuery.SQLText := SQLDataBaseConfig.GetDatabaseExistsQuery(vDatabaseName);
    vQuery.Open;
    result := not vQuery.Eof;
  finally
    ReleaseQuery(vQuery);
    FDConnection.Connected := False;
    FDConnection.Params.Database := vDatabaseName;
  end;
end;

destructor TBoldFireDACConnection.Destroy;
begin
  ReleaseCachedObjects;
  if fOwnsConnection then
    FreeAndNil(fFDConnection);
  inherited;
end;

procedure TBoldFireDACConnection.DropDatabase;
var
  vDatabaseName: string;
  vScript: TFDScript;
  sl: TStringList;
begin
  vDatabaseName := LowerCase(FDConnection.Params.Database);
  FDConnection.Params.Database := ''; // need to clear this to connect succesfully
  vScript := TFDScript.Create(nil);
  sl := TStringList.Create;
  try
    sl.Text := SQLDataBaseConfig.GetDropDatabaseQuery(vDatabaseName);
    vScript.Connection := FDConnection;
    vScript.ExecuteScript(sl);
    FDConnection.Close;
  finally
    FDConnection.Params.Database := vDatabaseName;
    vScript.free;
    sl.free;
  end;
end;

procedure TBoldFireDACConnection.EndExecuteQuery;
begin
  dec(fExecuteQueryCount);
end;

constructor TBoldFireDACConnection.Create(aFDConnection: TFDConnection; SQLDataBaseConfig: TBoldSQLDatabaseConfig);
begin
  inherited Create(SQLDataBaseConfig);
  fFDConnection := aFDConnection;
end;

function TBoldFireDACConnection.CreateAnotherDatabaseConnection: IBoldDatabase;
var
  Connection: TFDConnection;
  NewDbConnection: TBoldFireDACConnection;
begin
  Connection := TFDConnection.Create(nil);
  Connection.Assign(self.fFDConnection);
  NewDbConnection := TBoldFireDACConnection.Create(Connection, SQLDatabaseConfig);
  NewDbConnection.fOwnsConnection := True;
  result := NewDbConnection;
end;

procedure TBoldFireDACConnection.BeginExecuteQuery;
begin
  inc(fExecuteQueryCount);
end;

procedure TBoldFireDACConnection.Close;
begin
  FDConnection.Close;
end;

procedure TBoldFireDACConnection.CreateDatabase(DropExisting: boolean = true);
var
  vDatabaseName: string;
  vScript: TFDScript;
  sl: TStringList;
begin
  vDatabaseName := LowerCase(FDConnection.Params.Database);
  if DropExisting and DatabaseExists then
    DropDatabase;
  FDConnection.Params.Database := ''; // need to clear this to connect succesfully
  vScript := TFDScript.Create(nil);
  sl := TStringList.Create;
  try
    sl.Text := SQLDataBaseConfig.GetCreateDatabaseQuery(vDatabaseName);
    vScript.Connection := FDConnection;
    vScript.ExecuteScript(sl);
    FDConnection.Close;
  finally
    FDConnection.Params.Database := vDatabaseName;
    vScript.free;
    sl.free;
  end;
end;

function TBoldFireDACConnection.GetConnected: Boolean;
begin
  Result := FDConnection.Connected;
end;

function TBoldFireDACConnection.GetDatabaseError(const E: Exception;
  const sSQL: string): EBoldDatabaseError;
var
  vConnectionString: string;
begin
  vConnectionString := FDConnection.ConnectionString;
  Result := InternalGetDatabaseError(bdetError, E, vConnectionString, '', '', '', false);
end;

function TBoldFireDACConnection.GetExecQuery: IBoldExecQuery;
begin
  if Assigned(fCachedExecQuery1) then
  begin
    result := fCachedExecQuery1;
    fCachedExecQuery1 := nil;
  end else
  begin
    Result := BoldFireDACQueryClass.Create(Self);
  end;
end;

function TBoldFireDACConnection.GetFDConnection: TFDConnection;
begin
  Result := fFDConnection;
end;

function TBoldFireDACConnection.GetQuery: IBoldQuery;
begin
  if Assigned(fCachedQuery1) then
  begin
    result := fCachedQuery1;
    fCachedQuery1 := nil;
  end else
  if Assigned(fCachedQuery2) then
  begin
    result := fCachedQuery2;
    fCachedQuery2 := nil;
  end else
  begin
    Result := BoldFireDACQueryClass.Create(Self);
  end;
end;

function TBoldFireDACConnection.GetTable: IBoldTable;
var
  lFDTable: TFDTable;
begin
  if Assigned(fCachedTable) then
  begin
    result := fCachedTable;
    fCachedTable := nil;
  end
  else
  begin
    lFDTable := TFDTable.Create(nil);
    lFDTable.Connection := FDConnection;
    Result := TBoldFireDACTable.Create(lFDTable, Self);
  end;
end;

function TBoldFireDACConnection.GetTransaction: TFDTransaction;
begin
  if not Assigned(FDConnection.Transaction) then
    FDConnection.Transaction := TFDTransaction.Create(FDConnection);
  result := FDConnection.Transaction as TFDTransaction;
end;

function TBoldFireDACConnection.GetUpdateTransaction: TFDTransaction;
begin
  if not Assigned(FDConnection.UpdateTransaction) then
    FDConnection.UpdateTransaction := TFDTransaction.Create(FDConnection);
  result := FDConnection.UpdateTransaction as TFDTransaction;
end;

procedure TBoldFireDACConnection.Open;
begin
  try
    FDConnection.Params.Database := LowerCase(FDConnection.Params.Database);
    FDConnection.Open;
  except
    on E: Exception do begin
      raise GetDatabaseError(E);
    end;
  end;
end;

procedure TBoldFireDACConnection.Reconnect;
begin
  if Assigned(fFDConnection) then begin
    fFDConnection.Connected := False;
    fFDConnection.Connected := True;
  end;
end;

type TCollectionAccess = class(TCollection);

procedure TBoldFireDACConnection.ReleaseQuery(var Query: IBoldQuery);
var
  lBoldFireDACQuery: TBoldFireDACQuery;
begin
  if (Query.Implementor is TBoldFireDACQuery) then
  begin
    lBoldFireDACQuery := Query.Implementor as TBoldFireDACQuery;
    lBoldFireDACQuery.clear;
    while lBoldFireDACQuery.SQLStrings.Updating do
      lBoldFireDACQuery.SQLStrings.EndUpdate;
    while TCollectionAccess(lBoldFireDACQuery.Params).UpdateCount > 0 do
      lBoldFireDACQuery.Params.EndUpdate;
    Query := nil;
    if not Assigned(fCachedQuery1) then
      fCachedQuery1 := lBoldFireDACQuery
    else
    if not Assigned(fCachedQuery2) then
      fCachedQuery2 := lBoldFireDACQuery
    else
      lBoldFireDACQuery.free;
  end
end;

procedure TBoldFireDACConnection.ReleaseExecQuery(var Query: IBoldExecQuery);
var
  lBoldFireDACQuery: TBoldFireDACQuery;
//  lBoldFireDACExecQuery: TBoldFireDACExecQuery;
begin
  if (Query.Implementor is TBoldFireDACQuery) then
  begin
    lBoldFireDACQuery := Query.Implementor as TBoldFireDACQuery;
    if lBoldFireDACQuery.SQLStrings.Count <> 0 then
    begin
      lBoldFireDACQuery.SQLStrings.BeginUpdate;
      lBoldFireDACQuery.clear;
    end;
    while lBoldFireDACQuery.SQLStrings.Updating do
      lBoldFireDACQuery.SQLStrings.EndUpdate;
    while TCollectionAccess(lBoldFireDACQuery.Params).UpdateCount > 0 do
      lBoldFireDACQuery.Params.EndUpdate;
    Query := nil;
    if not Assigned(fCachedExecQuery1) then
      fCachedExecQuery1 := lBoldFireDACQuery
    else
      lBoldFireDACQuery.free;
  end;
end;

procedure TBoldFireDACConnection.ReleaseTable(var Table: IBoldTable);
var
  lBoldFireDACTable: TBoldFireDACTable;
begin
  if Table.Implementor is TBoldFireDACTable then
  begin
    lBoldFireDACTable := Table.Implementor as TBoldFireDACTable;
    Table := nil;
    if not Assigned(fCachedTable) then
      fCachedTable := lBoldFireDACTable
    else
      lBoldFireDACTable.free;
  end;
end;

function TBoldFireDACConnection.SupportsTableCreation: Boolean;
begin
  Result := False;
end;

{ TBoldFireDACParameter }

procedure TBoldFireDACParameter.Clear;
begin
  FDParam.Clear;
end;

constructor TBoldFireDACParameter.Create(FireDACParameter: TFireDacParam; DatasetWrapper: TBoldAbstractQueryWrapper);
begin
  inherited Create(DatasetWrapper);
  fFDParam := FireDACParameter;
end;

function TBoldFireDACParameter.GetAsAnsiString: TBoldAnsiString;
begin
  Result := FDParam.AsAnsiString;
end;

function TBoldFireDACParameter.GetAsBCD: Currency;
begin
  Result := FDParam.AsBCD;
end;

function TBoldFireDACParameter.GetAsblob: TBoldBlobData;
begin
  Result := AnsiString(FDParam.Value);
end;

function TBoldFireDACParameter.GetAsBoolean: Boolean;
begin
  Result := FDParam.AsBoolean;
end;

function TBoldFireDACParameter.GetAsCurrency: Currency;
begin
  Result := FDParam.AsCurrency;
end;

function TBoldFireDACParameter.GetAsDateTime: TDateTime;
begin
  Result := FDParam.AsDateTime;
end;

function TBoldFireDACParameter.GetAsFloat: Double;
begin
  Result := FDParam.AsFloat;
end;

function TBoldFireDACParameter.GetAsInt64: Int64;
begin
  result := FDParam.AsLargeInt;
end;

function TBoldFireDACParameter.GetAsInteger: Longint;
begin
  Result := FDParam.AsInteger;
end;

function TBoldFireDACParameter.GetAsMemo: string;
begin
  Result := String(FDParam.AsMemo);
end;

function TBoldFireDACParameter.GetAsString: string;
begin
  Result := FDParam.AsString;
  if Result = DatasetWrapper.DatabaseWrapper.SQLDataBaseConfig.EmptyStringMarker then
  begin
    Result := '';
  end;
end;

function TBoldFireDACParameter.GetAsVariant: Variant;
begin
  Result := FDParam.Value;
end;

function TBoldFireDACParameter.GetAsWideString: WideString;
begin
  Result := FDParam.AsWideString;
  if Result = DatasetWrapper.DatabaseWrapper.SQLDataBaseConfig.EmptyStringMarker then
  begin
    Result := '';
  end;
end;

function TBoldFireDACParameter.GetDataType: TFieldType;
begin
  Result := FDParam.DataType;
end;

function TBoldFireDACParameter.GetIsNull: Boolean;
begin
  Result := VarIsNull(FDParam.Value)
end;

function TBoldFireDACParameter.GetName: string;
begin
  Result := FDParam.Name;
end;

function TBoldFireDACParameter.GetFDParam: TFireDacParam;
begin
  Result := fFDParam;
end;

procedure TBoldFireDACParameter.SetAsAnsiString(const Value: TBoldAnsiString);
begin
  FDParam.AsAnsiString := Value;
end;

procedure TBoldFireDACParameter.SetAsBCD(const Value: Currency);
begin
  FDParam.Value := Value;
end;

procedure TBoldFireDACParameter.SetAsBlob(const Value: TBoldBlobData);
begin
  if FDParam.DataType = ftUnknown then
  begin
    FDParam.DataType := ftBlob;
  end;
  if Value = '' then
  begin
//    FDParam.Value := DatasetWrapper.DatabaseWrapper.SQLDataBaseConfig.EmptyStringMarker;
    FDParam.Value := Null;
  end else
  begin
    FDParam.Value := TBoldBlobData(AnsiString(Value));
  end;
end;

procedure TBoldFireDACParameter.SetAsBoolean(Value: Boolean);
begin
  FDParam.AsBoolean := Value;
end;

procedure TBoldFireDACParameter.SetAsCurrency(const Value: Currency);
begin
  FDParam.AsCurrency := Value;
end;

procedure TBoldFireDACParameter.SetAsDate(const Value: TDateTime);
begin
  FDParam.AsDate := Value;
end;

procedure TBoldFireDACParameter.SetAsDateTime(const Value: TDateTime);
begin
  FDParam.AsDateTime := Value;
end;

procedure TBoldFireDACParameter.SetAsFloat(const Value: Double);
begin
  FDParam.AsFloat := Value;
end;

procedure TBoldFireDACParameter.SetAsInt64(const Value: Int64);
begin
  FDParam.AsLargeInt := Value;
end;

procedure TBoldFireDACParameter.SetAsInteger(Value: Integer);
begin
  FDParam.AsInteger := Value;
end;

procedure TBoldFireDACParameter.SetAsMemo(const Value: string);
begin
  FDParam.AsMemo := AnsiString(Value);
end;

procedure TBoldFireDACParameter.SetAsSmallInt(Value: Integer);
begin
  FDParam.AsSmallInt := Value;
end;

procedure TBoldFireDACParameter.SetAsString(const Value: string);
begin
  if Value = '' then
  begin
    FDParam.AsString := DatasetWrapper.DatabaseWrapper.SQLDataBaseConfig.EmptyStringMarker;
  end else
  begin
    FDParam.AsString := Value;
  end;
end;

procedure TBoldFireDACParameter.SetAsTime(const Value: TDateTime);
begin
  FDParam.AsTime := Value;
end;

procedure TBoldFireDACParameter.SetAsVariant(const NewValue: Variant);
begin
  FDParam.Value := NewValue;
end;

procedure TBoldFireDACParameter.SetAsWideString(const Value: Widestring);
begin
  if Value = '' then
  begin
    FDParam.AsString := DatasetWrapper.DatabaseWrapper.SQLDataBaseConfig.EmptyStringMarker;
  end else
  begin
    FDParam.AsWideString := Value;
  end;
end;

procedure TBoldFireDACParameter.SetAsWord(Value: Integer);
begin
  FDParam.AsWord := Value;
end;

procedure TBoldFireDACParameter.SetDataType(Value: TFieldType);
begin
  FDParam.DataType := Value;
end;

procedure TBoldFireDACParameter.SetText(const Value: string);
begin
  FDParam.Value := Value;
end;

procedure TBoldFireDACParameter.Assign(const source: IBoldParameter);
begin
  FDParam.Value := Source.AsVariant;
end;

procedure TBoldFireDACParameter.AssignFieldValue(const source: IBoldField);
begin
  FDParam.Assign(source.Field);
end;

procedure TBoldFireDACConnection.ReleaseCachedObjects;
begin
  FreeAndNil(fCachedTable);
  FreeAndNil(fCachedQuery1);
  FreeAndNil(fCachedQuery2);
  FreeAndNil(fCachedExecQuery1);
end;

{ TBoldFireDACExecQuery }

procedure TBoldFireDACExecQuery.AssignParams(Sourceparams: TParams);
var
  lIndexSourceParams: Integer;
  lFDParam: TFireDacParam;
begin
  ExecQuery.Params.Clear;
  if Assigned(Sourceparams) and (Sourceparams.Count > 0) then
  begin
    for lIndexSourceParams := 0 to Sourceparams.Count - 1 do
    begin
      lFDParam := ExecQuery.Params.CreateParam(Sourceparams[lIndexSourceParams].DataType, Sourceparams[lIndexSourceParams].Name, Sourceparams[lIndexSourceParams].ParamType) as TFireDacParam;
      lFDParam.Value := Sourceparams[lIndexSourceParams].Value;
    end;
  end;
end;

procedure TBoldFireDACExecQuery.AssignSQL(SQL: TStrings);
begin
  ExecQuery.SQL.BeginUpdate;
  ExecQuery.SQL.Assign(SQL);
  ExecQuery.SQL.EndUpdate;
end;

procedure TBoldFireDACExecQuery.AssignSQLText(const SQL: string);
var
  lStringList: TStringList;
  lGuard: IBoldGuard;
begin
  lGuard := TBoldGuard.Create(lStringList);
  lStringList := TStringList.Create;
  lStringList.Add(SQL);
  AssignSQL(lStringList);
end;

procedure TBoldFireDACExecQuery.BeginExecuteQuery;
begin
  (DatabaseWrapper as TBoldFireDACConnection).BeginExecuteQuery;
end;

procedure TBoldFireDACExecQuery.Clear;
begin
  inherited;
  AssignSQLText('');
  ClearParams;
end;

procedure TBoldFireDACExecQuery.ClearParams;
begin
  ExecQuery.Params.Clear;
end;

constructor TBoldFireDACExecQuery.Create(BoldFireDACConnection: TBoldFireDACConnection);
begin
  inherited Create(BoldFireDACConnection);
  fUseReadTransactions := true;
end;

function TBoldFireDACExecQuery.CreateParam(FldType: TFieldType;
  const ParamName: string): IBoldParameter;
begin
  result := CreateParam(FldType, ParamName, ptUnknown, 0);
end;

function TBoldFireDACExecQuery.CreateParam(FldType: TFieldType; const ParamName: string; ParamType: TParamType; Size: integer): IBoldParameter;
var
  lFDParam: TFireDacParam;
begin
  lFDParam := ExecQuery.Params.CreateParam(FldType, ParamName, ptUnknown) as TFireDacParam;
  lFDParam.Size := Size;
  lFDParam.Value := NULL;
  Result := TBoldFireDACParameter.Create(lFDParam, Self);
end;

destructor TBoldFireDACExecQuery.Destroy;
begin
  FreeAndNil(fExecQuery);
  inherited;
end;

procedure TBoldFireDACExecQuery.EndExecuteQuery;
begin
  (DatabaseWrapper as TBoldFireDACConnection).EndExecuteQuery;
end;

function TBoldFireDACExecQuery.EnsureParamByName(
  const Value: string): IBoldParameter;
var
  lFDParam: TFireDacParam;
begin
  lFDParam := ExecQuery.Params.FindParam(Value);
  if not Assigned(lFDParam) then
    lFDParam := ExecQuery.Params.CreateParam(ftUnknown, Value, ptUnknown) as TFireDacParam;
  Result := TBoldFireDACParameter.Create(lFDParam, Self)
end;

procedure TBoldFireDACExecQuery.ExecSQL;
begin
  BeginExecuteQuery;
  try
    BoldLogSQLWithParams(ExecQuery.SQL, self);
    try
      if (DatabaseWrapper as TBoldFireDACConnection).GetInTransaction then
        fReadTransactionStarted := false
      else
      begin
        if fUseReadTransactions then
          (DatabaseWrapper as TBoldFireDACConnection).StartReadTransaction;
        fReadTransactionStarted := fUseReadTransactions;
      end;
      ExecQuery.Execute;
      if fReadTransactionStarted and (DatabaseWrapper as TBoldFireDACConnection).GetInTransaction then
      begin
        (DatabaseWrapper as TBoldFireDACConnection).Commit;
        fReadTransactionStarted := false;
      end;
    except
      on E: Exception do
      begin
        if (DatabaseWrapper as TBoldFireDACConnection).GetInTransaction then
          (DatabaseWrapper as TBoldFireDACConnection).Rollback;
        raise TBoldFireDACConnection(DatabaseWrapper).GetDatabaseError(E, ExecQuery.SQL.Text);
      end;
    end;
  finally
    EndExecuteQuery;
  end;
end;

function TBoldFireDACExecQuery.FindParam(const Value: string): IBoldParameter;
var
  Param: TFDParam;
begin
  Param := ExecQuery.FindParam(Value);
  if not Assigned(Param) then
    result := CreateParam(ftUnknown, Value);
end;

function TBoldFireDACExecQuery.GetBatchQueryParamCount: integer;
begin
  result := 0; // update when batch support is implemented
end;

function TBoldFireDACExecQuery.GetExecQuery: TFDQuery;
begin
  if not Assigned(fExecQuery) then
  begin
    fExecQuery := TFDQuery.Create(nil);
    fExecQuery.Connection := (DatabaseWrapper as TBoldFireDACConnection).FDConnection;
  end;
  Result := fExecQuery;
end;

function TBoldFireDACExecQuery.GetDataSet: TDataSet;
begin
  Result := ExecQuery;
end;

function TBoldFireDACExecQuery.GetParamCheck: Boolean;
begin
  result := ExecQuery.ResourceOptions.ParamCreate;
end;

function TBoldFireDACExecQuery.GetParamCount: Integer;
begin
  result := ExecQuery.Params.Count;
end;

function TBoldFireDACExecQuery.GetParams: TParams;
begin
  result := TFDAdaptedDataSetAccess(ExecQuery).fVclParams;
end;

function TBoldFireDACExecQuery.GetParam(i: Integer): IBoldParameter;
begin
  Result := TBoldFireDACParameter.Create(ExecQuery.Params[i], Self);
end;

function TBoldFireDACExecQuery.GetRowsAffected: Integer;
begin
  Result := ExecQuery.RowsAffected;
end;

function TBoldFireDACExecQuery.GetSQLStrings: TStrings;
begin
  result := ExecQuery.SQL;
end;

function TBoldFireDACExecQuery.GetSqlText: string;
begin
  Result := ExecQuery.SQL.Text;
end;

function TBoldFireDACExecQuery.GetUseReadTransactions: boolean;
begin
  result := fUseReadTransactions;
end;

function TBoldFireDACExecQuery.ParamByName(const Value: string): IBoldParameter;
var
  lFDParam: TFireDacParam;
begin
  lFDParam := ExecQuery.Params.ParamByName(Value);
  if Assigned(lFDParam) then
  begin
    Result := TBoldFireDACParameter.Create(lFDParam, Self)
  end else
  begin
    Result := nil;
  end;
end;

procedure TBoldFireDACExecQuery.Prepare;
begin
  ExecQuery.Prepare;
end;

procedure TBoldFireDACExecQuery.SetParamCheck(value: Boolean);
begin
  ExecQuery.ResourceOptions.ParamCreate := Value;
end;

procedure TBoldFireDACExecQuery.SetUseReadTransactions(value: boolean);
begin
  fUseReadTransactions := value;
end;

end.
